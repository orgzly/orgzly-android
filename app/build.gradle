apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


android {
    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }

    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16 // Jelly Bean (4.1)
        targetSdkVersion 29 // Android 10
        applicationId "com.orgzly"
        versionCode 155
        versionName "1.8.4"

        testInstrumentationRunner "com.orgzly.android.OrgzlyTestRunner"
        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        buildConfigField "String", "DROPBOX_APP_KEY", gradle.ext.appProperties.getProperty("dropbox.app_key", '""')
        resValue "string", "dropbox_app_key_schema", gradle.ext.appProperties.getProperty("dropbox.app_key_schema", '')

        buildConfigField "boolean", "IS_GIT_ENABLED", gradle.ext.appProperties.getProperty("git.enabled", 'false')

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true"
                ]
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

//    testOptions {
//        execution 'ANDROIDX_TEST_ORCHESTRATOR'
//    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG_DEBUG", "false"
        }

        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"

            buildConfigField "String", "DROPBOX_TOKEN", gradle.ext.appProperties.getProperty("dropbox.token", '""')
        }
    }

    flavorDimensions "store"

    productFlavors {
        premium {
            buildConfigField "boolean", "IS_DROPBOX_ENABLED", "true"

            buildConfigField "String", "VERSION_NAME_SUFFIX", '""'

            dimension "store"
        }

        fdroid {
            /*
             * Disable Dropbox.
             * Properties file which contains the required API key is not included with the code.
             */
            buildConfigField "boolean", "IS_DROPBOX_ENABLED", "false"

            buildConfigField "String", "VERSION_NAME_SUFFIX", '" (fdroid)"'

            dimension "store"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'MissingQuantity'
        disable 'ImpliedQuantity'

        /*
         * Added for:
         * Invalid package reference in library; not included in Android:
         * javax.servlet.http. Referenced from com.dropbox.core.DbxStandardSessionStore.
         */
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation orgJavaLocation()

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    implementation "org.jetbrains:annotations:$versions.jetbrains_annotations"

    implementation "androidx.multidex:multidex:$versions.android_multidex"

    implementation "androidx.appcompat:appcompat:$versions.android_appcompat"

    implementation "androidx.documentfile:documentfile:$versions.android_documentfile"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:$versions.android_localbroadcastmanager"

    // UI
    implementation "androidx.cardview:cardview:$versions.android_cardview"
    implementation "androidx.recyclerview:recyclerview:$versions.android_recyclerview"
    implementation "androidx.viewpager:viewpager:$versions.android_viewpager"
    implementation "androidx.constraintlayout:constraintlayout:$versions.android_constraint_layout"
    implementation "androidx.preference:preference:$versions.android_preference"
    implementation "com.google.android.material:material:$versions.android_material"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions.android_swiperefreshlayout"

    // Room
    implementation "androidx.room:room-runtime:$versions.android_room"
    testImplementation "androidx.room:room-testing:$versions.android_room"
    kapt "androidx.room:room-compiler:$versions.android_room"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.android_lifecycle"

    androidTestImplementation "androidx.annotation:annotation:$versions.android_annotation"

    testImplementation "junit:junit:$versions.junit"

    // AndroidX Test
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.android_test_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$versions.android_test_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$versions.android_test_espresso"
    androidTestImplementation "androidx.test:runner:$versions.android_test"
    androidTestImplementation "androidx.test:rules:$versions.android_test"
    androidTestImplementation "androidx.test.ext:junit:$versions.android_test_ext_junit"

    // For ANDROIDX_TEST_ORCHESTRATOR
    // androidTestUtil "androidx.test:orchestrator:$android_test_version"

    androidTestImplementation "androidx.test.uiautomator:uiautomator:$versions.android_test_uiautomator"

    androidTestImplementation "de.sven-jacobs:loremipsum:$versions.loremipsum"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    implementation "com.google.code.gson:gson:$versions.gson"

    implementation "com.dropbox.core:dropbox-core-sdk:$versions.dropbox_core_sdk"

    implementation "com.googlecode.juniversalchardet:juniversalchardet:$versions.juniversalchardet"

    implementation "com.evernote:android-job:$versions.evernote_android_job"

    implementation "joda-time:joda-time:$versions.joda_time"

    implementation "com.github.bumptech.glide:glide:$versions.glide"

    implementation("com.thegrizzlylabs.sardine-android:sardine-android:$versions.sardine") {
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation("org.eclipse.jgit:org.eclipse.jgit:$versions.jgit") {
        // Resolves DuplicatePlatformClasses lint error
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

repositories {
    mavenCentral()
}

/*
 * If "orgJavaDir" property is set, load org-java as a module. If property is not set,
 * use org-java from Maven repository. See settings.gradle.
 */
def orgJavaLocation() {
    if (gradle.ext.has('orgJavaDir')) {
        logger.info("app: Using org-java from ${gradle.ext.orgJavaDir}")
        return project(':org-java')
    } else {
        logger.info("app: Using com.orgzly:org-java:$versions.org_java from Maven repository")
        return "com.orgzly:org-java:$versions.org_java"
    }
}
